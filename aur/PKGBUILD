# Maintainer: mazix <mazix@bk.ru>
pkgname=perplexity
pkgver=1.1.3
pkgrel=1
pkgdesc="Native Perplexity AI client for Linux (Electron wrapper)"
arch=('x86_64')
url="https://github.com/mazix/perplexity"
license=('MIT')
depends=('electron')
makedepends=('npm')

# These files are expected to be in the same directory as the PKGBUILD
source=("launcher.sh"
        "perplexity.desktop"
        "perplexity.png"
        "default.conf")

sha256sums=('e407cdc386b008805b5b458135623b4373a6e166dca02fc200514d34d28d712c'
            '61e38f97079451a9a59125232fac460ee38d9c478c893cbf7fd2d6afbd61c9fd'
            '36ee4908a30561ee36454cc2319dbb636d6b0091c8ab46bc440ba25ce47a746c'
            'd3c83b2c03ac67e3bd45601004da2a1d4020e2d5540ca6b2887835e077edfecd')

prepare() {
    # Copy the application source code from the parent directory
    cp -r "${startdir}/../src" "${srcdir}/src"
    # Copy the license
    cp "${startdir}/../LICENSE" "${srcdir}/LICENSE"
}

build() {
    cd "${srcdir}/src"
    npm install --production
}

package() {
    # Install application files
    install -d "${pkgdir}/usr/lib/${pkgname}"
    cp -r "${srcdir}/src/"* "${pkgdir}/usr/lib/${pkgname}/"

    # Install launcher script
    install -Dm755 "${srcdir}/launcher.sh" "${pkgdir}/usr/bin/${pkgname}"

    # Install desktop entry
    install -Dm644 "${srcdir}/perplexity.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

    # Install icon
    install -Dm644 "${srcdir}/perplexity.png" "${pkgdir}/usr/share/icons/hicolor/512x512/apps/${pkgname}.png"

    # Install config file
    install -Dm644 "${srcdir}/default.conf" "${pkgdir}/etc/${pkgname}/default.conf"

    # Install license
    install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
